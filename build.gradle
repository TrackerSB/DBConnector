plugins {
    id "java-library"
    id "org.openjfx.javafxplugin" version "0.0.10"
    id "maven-publish"
    id "signing"
}

group "bayern.steinbrecher"
version "0.16-rc.72-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'bayern.steinbrecher:JavaUtility:0.18'
    implementation 'bayern.steinbrecher:jsch:0.1.57'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.6'
    implementation 'org.freemarker:freemarker:2.3.31'
    implementation 'org.jetbrains:annotations:23.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.craftercms.mariaDB4j:mariaDB4j:2.5.3'
    testRuntimeOnly 'org.craftercms.mariaDB4j:mariaDB4j:2.5.0'
    testImplementation 'org.craftercms.mariaDB4j:mariaDB4j-db-win64:10.4.6.2'
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    modularity.inferModulePath = true
    withSourcesJar()
    withJavadocJar()
}

sourceCompatibility = 17
targetCompatibility = "$sourceCompatibility" as Integer

javafx {
    version = "$sourceCompatibility"
    modules = [
            "javafx.base",
            "javafx.controls"
    ]
}

test {
    useJUnitPlatform()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${ossrhUsername}"
                password = "${ossrhPassword}"
            }
        }
    }
    publications {
        library(MavenPublication) {
            pom {
                name = 'DBConnector'
                description = 'A library which aims to simplify database connections especially if they are over SSH'
                url = 'https://steinbrecher-bayern.de'
                licenses {
                    license {
                        name = 'The Beer-Ware License (Revision 42)'
                    }
                }
                developers {
                    developer {
                        id = 'stefanHuber'
                        name = 'Stefan Huber'
                        email = 'stefan.huber.niedling@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TrackerSB/DBConnector.git'
                    developerConnection = 'scm:git:ssh://github.com/TrackerSB/DBConnector.git'
                    url = 'https://github.com/TrackerSB/DBConnector'
                }
            }
            from components.java
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.library
}
